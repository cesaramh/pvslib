differential_variant  % [ parameters ]
		: THEORY

  BEGIN

  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  %| The proof rule for the eventuality property of a   |%
  %| hybrid system. The conditions involving the set J  |%
  %| define is as a staging set for the system with     |%
  %| domain R, initial set P, and target set Q. The     |%
  %| conditions involving the function Progress defines |%
  %| it as a progress function, guarenteeing the        |%
  %| solution will exit the staging set by way of the   |%
  %| target set.                                        |%
  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  % Author: LMW


  IMPORTING dL@dynamic_logic,
  	        analytic_equiv


  % Differential Variant
  % (P /\ NOT Q |- J)
  %   (J |- [x' = f(x) & NOT(R /\ Q)]J)
  %     (P OR J |- R)
  %       (|- EXISTS (eps > 0): FORALL x: J -> (Progress(x) >= 0 AND L_f(Progress) <= -eps)
  % -----------------------------------------------------------------------------------------
  %              P |- <x' = f(x) & R> Q

  l,m: VAR Assigns
  re:  VAR RealExpr
  r :  VAR real

  Gamma,Delta : VAR Formulas
  P,Q,J,R     : VAR BoolExpr
  A,B         : VAR HP
  qQ          : VAR QBoolExpr
  Progress    : VAR RealExpr
  re1,re2     : VAR RealExpr

  % @QED dl_dV proved by lmwhite3 on Fri, 02 Sep 2022 14:45:52 GMT
  dl_dV: LEMMA
    FORALL (PP:(ddl_dom?))(Gamma,Delta)
    (ode:(ODEs_P_analytic?(hp(0),PP)))(J:(SA?),Q,R,P)
    (Progress:(diff_re?(max_var(ode),PP))):
      (SA?(DLNOT(DLAND(R,Q))) AND ((DLAND(P,DLNOT(Q)) |- J) AND
       (J |- ALLRUNS(DIFF(ode,DLAND(PP,DLNOT(DLAND(R,Q)))),J)) AND
       (DLOR(P,J) |- R) AND
       EXISTS(eps:posreal): (null |-
        DLIMPLIES(J,DLAND(Progress >= 0,SUB_DIFTRe(PP,ode)(Progress) <= cnst(-eps)))))) IMPLIES
      (cons(P,Gamma) |- cons(SOMERUNS(DIFF(ode,DLAND(PP,R)),Q),Delta))

  % @QED dl_dV_targetSA proved by lmwhite3 on Fri, 29 Apr 2022 15:40:12 GMT
  dl_dV_targetSA: LEMMA
    FORALL (PP:(ddl_dom?))(Gamma,Delta)
    (ode:(ODEs_P_analytic?(hp(0),PP)))(J,Q,R:(SA?),P)
    (Progress:(diff_re?(max_var(ode),PP))):
      (((DLAND(P,DLNOT(Q)) |- J) AND
       (J |- ALLRUNS(DIFF(ode,DLAND(PP,DLNOT(DLAND(R,Q)))),J)) AND
       (DLOR(P,J) |- R) AND
       EXISTS(eps:posreal): (null |-
        DLIMPLIES(J,DLAND(Progress >= 0,SUB_DIFTRe(PP,ode)(Progress) <= cnst(-eps)))))) IMPLIES
      (cons(P,Gamma) |- cons(SOMERUNS(DIFF(ode,DLAND(PP,R)),Q),Delta))




  END differential_variant
