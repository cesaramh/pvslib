sem_rel_diff_star: THEORY % Welcome

%Created by                     LW
%	 	           Sept 2023
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| Allows the removal of the
%| STAR operator on a DIFF
%| Hybrid program
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Last updated	          Sept 2023


%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Importing dl top to get Plaidypvs
%-------------------------------------------------------------------------

  IMPORTING bounded_star_semantics,
            solution_odes_props

%-------------------------------------------------------------------------
%% Bstar can be removed from DIFF hybrid programs
%-------------------------------------------------------------------------
sem_rel_diff_hp_bstar: LEMMA
FORALL(odes:ODEs,P:BoolExpr,envi,envo:Environment):
FORALL(fs: (solution_odes_u?(hp(0),odes,envi))):
FORALL(i:nat):
P(envi) AND (semantic_rel(BSTAR(DIFF(odes,P),i))(envi)(envo))
IMPLIES
semantic_rel(DIFF(odes,P))(envi)(envo)

%-------------------------------------------------------------------------
%% Bstar can be removed from DIFF hybrid programs (converse)
%-------------------------------------------------------------------------

sem_rel_diff_hp_bstar_con: LEMMA
FORALL(odes:ODEs,P:BoolExpr,envi,envo:Environment):
FORALL(fs: (solution_odes_u?(hp(0),odes,envi))):
semantic_rel(DIFF(odes,P))(envi)(envo)
IMPLIES
EXISTS(i:nat):
(semantic_rel(BSTAR(DIFF(odes,P),i))(envi)(envo))

%-------------------------------------------------------------------------
%% STAR can be removed from DIFF hybrid programs
%-------------------------------------------------------------------------
sem_rel_diff_hp: LEMMA
FORALL(odes:ODEs,P:BoolExpr,envi,envo:Environment):
FORALL(fs: (solution_odes_u?(hp(0),odes,envi))):
(P(envi) AND semantic_rel(STAR(DIFF(odes,P)))(envi)(envo))
IFF
semantic_rel(DIFF(odes,P))(envi)(envo)

%-------------------------------------------------------------------------
%% STAR can be removed from DIFF hybrid programs special case 
%-------------------------------------------------------------------------
sem_rel_diff_hp_env: LEMMA
FORALL(odes:ODEs,P:BoolExpr,envi,envo:Environment):
FORALL(fs: (solution_odes_u?(hp(0),odes,envi))):
envi /= envo IMPLIES
(semantic_rel(STAR(DIFF(odes,P)))(envi)(envo)
IFF
semantic_rel(DIFF(odes,P))(envi)(envo))

%-------------------------------------------------------------------------
%% STAR can be removed from DIFF hybrid programs (converse)
%-------------------------------------------------------------------------
star_diff_hp: LEMMA
FORALL(odes:ODEs,P:BoolExpr,Q:BoolExpr):
(EXISTS(fs:[Environment -> [ below(length(odes)) -> [real -> real] ] ]):
(FORALL(env:Environment): solution_odes_u?(hp(0),odes,env)(fs(env))))
IMPLIES
DLAND(Q,ALLRUNS(STAR(DIFF(odes,Q)),P))=DLAND(Q,ALLRUNS(DIFF(odes,Q),P))

%| “There’s as many atoms in a single molecule of your DNA
%|  as there are stars in the typical galaxy.
%   We are, each of us, a little universe.”
%                                    – Neil deGrasse Tyson
  %~~~     The End ~~~%
  END sem_rel_diff_star
