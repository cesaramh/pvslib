derivatives_shift   % Welcome
      	      : THEORY
	      
%Created by         J Tanner Slagel
%                          Aug 2023
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%| This relates the derivative   |%
%| of a function with the        |%
%| derivative of the function    |%
%| with a shift                  |%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%Lase Updated by                JTS
%	                   Aug 2023
         

%-----     %
  BEGIN
%     -----%

%-------------------------------------------------------------------------
%% Importing derivatives subtype
%-------------------------------------------------------------------------

IMPORTING derivatives_subtype,
	  deriv_domain

%-------------------------------------------------------------------------
%% Define a shifted domain to the right by a real number a 
%-------------------------------------------------------------------------

shft(d:[real->bool], a:real)(x:real): bool =
 d(x+a)

%-------------------------------------------------------------------------
%% deriv domain and not one element are preserved through shift
%-------------------------------------------------------------------------

shift_dd: LEMMA
 FORALL(d:[real -> bool], a:real):
 deriv_domain?[(d)]
 IMPLIES
 deriv_domain?[(shft(d,a))]

shift_noe: LEMMA
 FORALL(d:[real -> bool], a:real):
 not_one_element?[(d)]
 IMPLIES
 not_one_element?[(shft(d,a))]

%-------------------------------------------------------------------------
%% Define the shifted function to the right by a 
%-------------------------------------------------------------------------

shft(d:[real->bool],a:real,f:[(d)->real])
 (x:(shft(d,a))): real = f(x+a)



%-------------------------------------------------------------------------
%% The shift of a differentiable function is still differentiable 
%-------------------------------------------------------------------------

derivable_shift: LEMMA
 FORALL(d:[real->bool], a:real,
  f:[(d) -> real],x:(d)):
  deriv_domain?[(d)]
  IMPLIES
  (derivable?[(d)](f,x)
  IMPLIES
  derivable?[(shft(d,a))](shft(d,a,f),x-a))

%-------------------------------------------------------------------------
%% The correspondance between the derivative of a function
%  and the derivative of its shift
%-------------------------------------------------------------------------

deriv_shift: LEMMA
 FORALL(d:[real->bool], a:real,
  f:[(d) -> real],x:(d)):
  deriv_domain?[(d)]
  IMPLIES
  (derivable?[(d)](f,x)
  IMPLIES
  deriv[(d)](f,x) = deriv[(shft(d,a))](shft(d,a,f),x-a))
 
deriv_shift_f: LEMMA
 FORALL(d:[real->bool], a:real,
  f:[(d) -> real]):
  deriv_domain?[(d)]
  IMPLIES
  (derivable?[(d)](f)
  IMPLIES
  derivable?[(shft(d,a))](shft(d,a,f)) AND 
  (deriv[(d)](f) = (LAMBDA(x:(d)): deriv[(shft(d,a))](shft(d,a,f))(x-a))))
  
%- ``When we shift our perception,
%-    our experience changes.''
%-                -Lindsay Wagner

  %~~~ The End     ~~~%
  END derivatives_shift
