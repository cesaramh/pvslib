aerr754dp_div: THEORY
BEGIN

  IMPORTING ieee754dp_div
  IMPORTING accum_err_div[ieee754radix,double_precision_format,dp_closest?,RtoD]

  e1,e2 : VAR nonneg_real
  r1,r2 : VAR real
  
  aebounddp_div(r1,e1,r2,e2): nonneg_real =
    IF (0<r2-e2 OR r2+e2<0)
    THEN div_aerr_bound(r1,e1,r2,e2) + ulp_dp(div_ulp_bound(r1,e1,r2,e2))/2
    ELSE 0
    ENDIF
    
  Ddiv_aerr: LEMMA
   FORALL(e1, e2: nonneg_real, r1, r2: real, f1, f2: unb_double):
    abs(DtoR(f1)-r1) <= e1 AND
    abs(DtoR(f2)-r2) <= e2 AND
    (0<r2-e2 OR r2+e2<0)
    IMPLIES abs(DtoR(Ddiv(f1,f2))-(r1/r2)) <= aebounddp_div(r1,e1,r2,e2) 

%|- Ddiv_aerr : PROOF
%|- (then (expand "aebounddp_div") (skeep) (lift-if)
%|-  (spread (split 1)
%|-   ((then (flatten) (lemma "accum_err_bound") (expand "DtoR")
%|-     (expand "Ddiv" 1) (expand "ulp_dp") (inst?)
%|-     (spread (split -1) ((propax) (propax) (propax) (assert))))
%|-    (then (flatten) (assert)))))
%|- QED 


END aerr754dp_div
