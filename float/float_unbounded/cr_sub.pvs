%
% Definition of Subtraction operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
cr_sub[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING float[radix]

  f1,f2:    VAR float
  x1,x2,x3: VAR real

  trivial1(x1):     MACRO bool = TRUE    
  trivial2(x1)(x2): MACRO bool = TRUE

  IMPORTING binop_em_scheme[radix,b,round,RtoF,trivial1,trivial2,-]

  Fsub(f1,f2): {f3: (Fcanonic?(b)) | Closest?(b)(FtoR(f1)-FtoR(f2),f3)} =
    Fg(f1,f2)

  Fsub_bounded: LEMMA Fbounded?(b)(Fsub(f1,f2))
  AUTO_REWRITE+ Fsub_bounded

  Fsub_error: LEMMA 
    abs(FtoR(Fsub(f1,f2)) - (FtoR(f1)-FtoR(f2))) <= Fulp(b)(Fsub(f1,f2))/2
    
  %|- F*_error_ulp : PROOF
  %|- (then (skeep)(lemma "closest_ulp")(inst?)
  %|-   (spread (split -1)
  %|-    ((then (propax))
  %|-     (then (invoke (typepred "%1") (! 1 1))(use "FcanonicBounded")(assert))
  %|-     (then (invoke (typepred "%1") (! 1 2))))))
  %|- QED F*_error_ulp
  % @QED Fsub_error_ulp proved by mmoscato on Tue, 15 Nov 2022 03:10:01 GMT
  Fsub_error_ulp: LEMMA 
    abs(FtoR(Fsub(f1,f2)) - (FtoR(f1)-FtoR(f2)))
    <= ulp(b)(FtoR(f1)-FtoR(f2))/2
    
END cr_sub
