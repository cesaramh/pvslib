trig_fp_bounds: THEORY
BEGIN

  IMPORTING analysis@derivative_props[real],
  	    trig@trig_basic,
  	    trig@trig_extra,
  	    trig@trig_approx
 
  sin_error_prep : LEMMA 
  FORALL (x, eps: real):
  abs(sin(x+eps) - sin(x)) <= 2*abs(sin(abs(eps/2)))

  cos_error_prep : LEMMA
  FORALL (x, eps: real):
  abs(cos(x+eps) - cos(x)) <= 2*abs(sin(abs(eps/2)))

  sin_bound : LEMMA 
  FORALL (eps: nnreal):
  eps<= 4 IMPLIES
  2*abs(sin(eps/2))<= min(eps, eps- eps^3/24 + eps^5/1920 - eps^7/322560 + eps^9/92897280)

  sin_error_bound: LEMMA
  FORALL (x:real, (eps: real | abs(eps)<=4)):
  LET peps = abs(eps) IN
  abs(sin(x+eps) - sin(x)) <= min(peps, peps- peps^3/24 + peps^5/1920 - peps^7/322560 + peps^9/92897280)
  

  cos_error_bound: LEMMA
  FORALL (x:real, (eps: real | abs(eps)<=4)):
  LET peps = abs(eps) IN
  abs(cos(x+eps) - cos(x)) <= min(peps, peps- peps^3/24 + peps^5/1920 - peps^7/322560 + peps^9/92897280)

  sin_bnd_simple: LEMMA 
  FORALL (x,eps:real):
  abs(sin(x+eps)-sin(x))<= min(2, abs(eps))

  cos_bnd_simple: LEMMA 
  FORALL (x,eps:real):
  abs(cos(x+eps)-cos(x))<= min(2, abs(eps))

  sin_error_eps: LEMMA 
  FORALL (x, ep:real, eps:nnreal):
  abs(ep)<=eps IMPLIES
  abs(sin(x+ep)-sin(x))<= min(2, eps)

  cos_error_eps: LEMMA 
  FORALL (x, ep:real, eps:nnreal):
  abs(ep)<=eps IMPLIES
  abs(cos(x+ep)-cos(x))<= min(2, eps)

  atan_error_eps: LEMMA
  FORALL (x,ep:real, eps:nnreal):
  abs(ep)<=eps IMPLIES 
  abs(atan(x+ep)-atan(x))<= eps/(1+ IF abs(x)<=eps THEN 0 ELSE min((x-eps)^2, (x+eps)^2) ENDIF) 

  generic_bounding: LEMMA
  FORALL (F:[real->real], x, ep:real, eps:nnreal, bnd:[[real, nnreal]->nnreal]):
  abs(F(x+ep)-F(x))<=bnd(x, eps) 
  IMPLIES abs(F(x+ep))<= abs(F(x))+bnd(x, eps)

  sin_ulp_bounded: LEMMA
  FORALL (x, ep:real, eps:nnreal):
  abs(ep)<=eps IMPLIES
  abs(sin(x+ep))<= abs(sin(x))+min(2,eps) 

  cos_ulp_bounded: LEMMA
  FORALL (x, ep:real, eps:nnreal):
  abs(ep)<=eps IMPLIES
  abs(cos(x+ep))<= abs(cos(x))+min(2,eps) 

  atan_ulp_bounded: LEMMA 
  FORALL (x, h:real, eps: nnreal):
  abs(h)<= eps IMPLIES
  abs(atan(x+h))<= atan(abs(x)+eps)

  % More props
  div_ulp: LEMMA
  FORALL (x1, x2, h1:real, ( h2:real | x2+h2 /=0 ), eps1:nnreal, (eps2: nnreal | (x2-eps2)*(x2+eps2)>0)):
  abs(h1)<=eps1 AND abs(h2)<=eps2
  IMPLIES
  abs((x1+h1)/(x2+h2))<= (abs(x1)+eps1)/(abs(x2)-eps2) 

  sqrt_ulp: LEMMA 
  FORALL (x:nnreal, (h:real | x+h>=0), eps: nnreal):
  abs(h)<= eps IMPLIES 
  abs(sqrt(x+h))<= sqrt(x+eps) 

  atan_ulp: LEMMA 
  FORALL (x, h:real, eps: nnreal):
  abs(h)<= eps IMPLIES
  abs(atan(x+h))<= atan(abs(x)+eps) 

END trig_fp_bounds
