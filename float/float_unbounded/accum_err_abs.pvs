accum_err_abs[
  radix : above(1),
  (IMPORTING float[radix])
  b     : Format,
  round: ClosestRoundingPred[radix](b),
  RtoF : { RtoF: CanonicRoundingFun(b,round) |
                 exact_rep_conservation[radix](b,round)(RtoF) }
]: THEORY
BEGIN

  IMPORTING cr_abs[radix,b,round,RtoF],
  	    reals@abs_lems

  f1 : VAR (Fbounded?(b))
  e1 : VAR nonneg_real
  r1 : VAR real

  abs_aerr_bound(r1,e1): real = e1

  abs_accum_err: LEMMA 
    abs(FtoR(f1)-r1) <= e1
    IMPLIES abs(abs(FtoR(f1)) - abs(r1)) <= abs_aerr_bound(r1,e1)

  IMPORTING accum_err_op1sch_exact
   [(Fbounded?(b)), FtoR, 
   LAMBDA(f1): TRUE, cr_abs.Fabs,
   LAMBDA(r1): TRUE, abs,
   LAMBDA(r1,e1): TRUE,
   abs_aerr_bound] AS aelemmath_exact_abs

  accum_err_abs: LEMMA
     abs(FtoR(f1)-r1) <= e1
     IMPLIES abs(FtoR(cr_abs.Fabs(f1)) - abs(r1))
               <= abs_aerr_bound(r1,e1)

END accum_err_abs
