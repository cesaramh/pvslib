% Definition of Cosine operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
std_cos[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING trig@trig_basic

  f1: VAR float
  x1: VAR real

  cos_pre(x1):MACRO bool = TRUE

  IMPORTING unop_em_scheme[radix,b,round,RtoF,cos_pre,cos]
  
  Fcos(f1): {f2: (Fcanonic?(b)) | Closest?(b)(cos(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fcos_bounded: LEMMA
    Fbounded?(b)(Fcos(f1))
  AUTO_REWRITE+ Fcos_bounded

  Fcos_error: LEMMA
    abs(FtoR(Fcos(f1))-(cos(FtoR(f1))))<=Fulp(b)(Fcos(f1))/2

  Fcos_error_ulp: LEMMA 
   FORALL(f1|cos_pre(FtoR(f1))):
    abs(FtoR(Fcos(f1)) - cos(FtoR(f1)))
    <= ulp(b)(cos(FtoR(f1)))/2

END std_cos
