% Definition of Arc Tangent operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
std_atn[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING trig@trig_basic,
  	    trig@atan

  f1: VAR float
  x1: VAR real

  atn_pre(x1):MACRO bool = TRUE

  IMPORTING unop_em_scheme[radix,b,round,RtoF,atn_pre,atan]
  
  Fatn(f1): {f2: (Fcanonic?(b)) | Closest?(b)(atan(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fatn_bounded: LEMMA
    Fbounded?(b)(Fatn(f1))
  AUTO_REWRITE+ Fatn_bounded

  Fatn_error: LEMMA
    abs(FtoR(Fatn(f1))-(atan(FtoR(f1))))<=Fulp(b)(Fatn(f1))/2

  Fatn_error_ulp: LEMMA 
   FORALL(f1|atn_pre(FtoR(f1))):
    abs(FtoR(Fatn(f1)) - atan(FtoR(f1)))
    <= ulp(b)(atan(FtoR(f1)))/2

END std_atn
