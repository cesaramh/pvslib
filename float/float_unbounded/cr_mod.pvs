% Definition of Modulo operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
cr_mod[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING reals@reals_mod

  f1,f2:    VAR float
  x1,x2,x3: VAR real

  mod_pre1(x1) : bool = TRUE
  mod_pre2(x1)(x2): bool = x2/=0

  IMPORTING binop_em_scheme[radix,b,round,RtoF,mod_pre1,mod_pre2,mod]

  Fmod(f1,(f2|mod_pre2(FtoR(f1))(FtoR(f2)))): {f3: (Fcanonic?(b)) |
  				   Closest?(b)(mod(FtoR(f1),FtoR(f2)),f3)} =
    Fg(f1,f2)
  
  Fmod_bounded: LEMMA
   FORALL(f2|mod_pre2(FtoR(f1))(FtoR(f2))):
    Fbounded?(b)(Fmod(f1,f2))
  AUTO_REWRITE+ Fmod_bounded

  Fmod_error: LEMMA 
   FORALL(f2|mod_pre2(FtoR(f1))(FtoR(f2))):
    abs(FtoR(Fmod(f1,f2))-(mod(FtoR(f1),FtoR(f2))))<=Fulp(b)(Fmod(f1,f2))/2
    
END cr_mod
