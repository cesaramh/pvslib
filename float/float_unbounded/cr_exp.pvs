% Definition of Exponentiation operation following the IEEE 754 standard. According
% to the standard, every operation must be performed as if it first produced
% an intermediate result correct to infinite precision and with unbounded range,
% and then rounded to fit the target format, if necessary." [IEEE Std 754-2008]
%
cr_exp[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING float[radix]
  
  f1,f2:    VAR float
  x1,x2,x3: VAR real

  exp_pre1(x1) : bool = TRUE
  exp_pre2(x1)(x2): bool = integer?(x2) AND (x1/=0 OR x2>=0)

  IMPORTING binop_em_scheme[radix,b,round,RtoF,exp_pre1,exp_pre2,^]

  Fexp(f1,(f2|exp_pre2(FtoR(f1))(FtoR(f2)))): {f3: (Fcanonic?(b)) |
  				            Closest?(b)(FtoR(f1)^FtoR(f2),f3)} =
    Fg(f1,f2)

  Fexp_bounded: LEMMA
   FORALL(f2|exp_pre2(FtoR(f1))(FtoR(f2))):
    Fbounded?(b)(Fexp(f1,f2))
  AUTO_REWRITE+ Fexp_bounded
  
  Fexp_error: LEMMA 
   FORALL(f2|exp_pre2(FtoR(f1))(FtoR(f2))):
    abs(FtoR(Fexp(f1,f2)) - (FtoR(f1)^FtoR(f2))) <= Fulp(b)(Fexp(f1,f2))/2
    
END cr_exp
