accum_err_neg[
  radix : above(1),
  (IMPORTING float[radix])
  b     : Format,
  round: ClosestRoundingPred[radix](b),
  RtoF : { RtoF: CanonicRoundingFun(b,round) |
                 exact_rep_conservation[radix](b,round)(RtoF) }
]: THEORY
BEGIN

  IMPORTING cr_neg[radix,b,round,RtoF]

  f1 : VAR (Fbounded?(b))
  e1 : VAR nonneg_real
  r1 : VAR real

  neg_aerr_bound(r1,e1): real = e1

  neg_accum_err: LEMMA 
    abs(FtoR(f1)-r1) <= e1
    IMPLIES abs((-FtoR(f1)) - (-r1)) <= neg_aerr_bound(r1,e1)

  IMPORTING accum_err_op1sch_exact
   [(Fbounded?(b)), FtoR, 
   LAMBDA(f1): TRUE, Fneg,
   LAMBDA(r1): TRUE, LAMBDA(r1): -r1,
   LAMBDA(r1,e1): TRUE,
   neg_aerr_bound] AS aelemmath_exact_neg

  accum_err_neg: LEMMA
     abs(FtoR(f1)-r1) <= e1
     IMPLIES abs(FtoR(Fneg(f1)) - (-r1))
               <= neg_aerr_bound(r1,e1)

END accum_err_neg
