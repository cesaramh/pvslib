% Definition of Natural Logarithm operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
std_ln[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  f1: VAR float
  x1: VAR real

  ln_pre(x1):MACRO bool = x1 > 0

  IMPORTING unop_em_scheme[radix,b,round,RtoF,ln_pre,ln]

  Fln((f1|ln_pre(FtoR(f1)))):
                        {f2: (Fcanonic?(b)) | Closest?(b)(ln(FtoR(f1)),f2) } =
    Fg(f1)    
    
  Fln_bounded: LEMMA
   FORALL(f1|ln_pre(FtoR(f1))):
    Fbounded?(b)(Fln(f1))
  AUTO_REWRITE+ Fln_bounded

  Fln_error: LEMMA
   FORALL(f1|ln_pre(FtoR(f1))):
    abs(FtoR(Fln(f1))-(ln(FtoR(f1))))<=Fulp(b)(Fln(f1))/2

  Fln_error_ulp: LEMMA 
   FORALL(f1|ln_pre(FtoR(f1))):
    abs(FtoR(Fln(f1)) - ln(FtoR(f1)))
    <= ulp(b)(ln(FtoR(f1)))/2

END std_ln
