accum_err_cos[
  radix : above(1),
  (IMPORTING float[radix])
  b     : Format,
  round: ClosestRoundingPred[radix](b),
  RtoF : { RtoF: CanonicRoundingFun(b,round) |
                 exact_rep_conservation[radix](b,round)(RtoF) }
]: THEORY
BEGIN
  ASSUMING

    round_even_closest: ASSUMPTION
      FORALL(x:real,f:(Fbounded?(b))): round(x,f) IMPLIES EvenClosest?(b)(x,f)

    round_rounded_mode: ASSUMPTION
      rounded_mode?(b)(round)

  ENDASSUMING

  f1 : VAR (Fbounded?(b))
  e1 : VAR nonneg_real
  r1 : VAR real

  IMPORTING std_cos[radix,b,round,RtoF],
  	    reals@abs_lems,
            interval_arith@interval_mps,
	    trig_fp_bounds

  cos_aerr_bound(r1,e1): nonneg_real = min(2, e1)

  cos_accum_err: LEMMA 
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(cos(FtoR(f1))-cos(r1)) <= cos_aerr_bound(r1,e1)

  cos_ulp_bound(r1,e1): real = abs(cos(r1))+min(2,e1)

  Fcos_accum_err_bound: LEMMA
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(cos(FtoR(f1))) <= cos_ulp_bound(r1,e1)  

  IMPORTING accum_err_op1sch[
              (Fbounded?(b)), FtoR, RtoF, ulp(b),
	      LAMBDA(f1): TRUE, Fcos,
	      LAMBDA(r1): TRUE, cos,
	      LAMBDA(r1,e1): TRUE,
	      cos_aerr_bound,
	      LAMBDA(r1,e1): TRUE,
	      cos_ulp_bound] AS cos_prf

  accum_err_bound: LEMMA
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(FtoR(Fcos(f1)) - cos(r1))
            <= cos_aerr_bound(r1,e1)
	       + ulp(b)(cos_ulp_bound(r1,e1)) / 2    

END accum_err_cos
