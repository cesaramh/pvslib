aerr754dp_mul: THEORY
BEGIN

  IMPORTING ieee754dp_mul,
  	    accum_err_mul[ieee754radix,double_precision_format,dp_closest?,RtoD]

  e1,e2 : VAR nonneg_real
  r1,r2 : VAR real
  
  aebounddp_mul(r1,e1,r2,e2): nonneg_real =
    mul_aerr_bound(r1,e1,r2,e2) + ulp_dp(mul_ulp_bound(r1,e1,r2,e2))/2
    
  Dmul_aerr: LEMMA
   FORALL(e1, e2: nonneg_real, r1, r2: real, f1, f2: unb_double):
    abs(DtoR(f1)-r1) <= e1 AND
    abs(DtoR(f2)-r2) <= e2
    IMPLIES abs(DtoR(Dmul(f1,f2))-(r1*r2)) <= aebounddp_mul(r1,e1,r2,e2)

  IMPORTING aerr_mul_props[ieee754radix,double_precision_format,dp_closest?,RtoD]

  aebounddp_mul_p2l(n: nat,e: nonneg_real): nonneg_real = 2^n * e
  aebounddp_mul_p2r(n: nat,e: nonneg_real): nonneg_real = 2^n * e

  Dmulpow2l_aerr: LEMMA
   FORALL(e: nonneg_real,
   	  r: real,
	  f: unb_double,
	  n: below(ieee754dp_prec) | n < ieee754dp_prec - Fexp(f)):
    abs(DtoR(f)-r) <= e
    IMPLIES abs(DtoR(Dmul(RtoD(2^n),f))-(2^n*r)) <= aebounddp_mul_p2l(n,e)

  Dmulpow2r_aerr: LEMMA
   FORALL(e: nonneg_real,
   	  r: real,
	  f: unb_double,
	  n: below(ieee754dp_prec) | n < ieee754dp_prec - Fexp(f)):
    abs(DtoR(f)-r) <= e
    IMPLIES abs(DtoR(Dmul(f,RtoD(2^n)))-(r*2^n)) <= aebounddp_mul_p2r(n,e)

END aerr754dp_mul
