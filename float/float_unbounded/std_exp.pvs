% Definition of Exponential operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
std_exp[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  f1: VAR float
  x1: VAR real

  exp_pre(x1):MACRO bool = TRUE

  IMPORTING unop_em_scheme[radix,b,round,RtoF,exp_pre,exp]
  
  Fexp(f1): {f2: (Fcanonic?(b)) | Closest?(b)(exp(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fexp_bounded: LEMMA
    Fbounded?(b)(Fexp(f1))
  AUTO_REWRITE+ Fexp_bounded

  Fexp_error: LEMMA
    abs(FtoR(Fexp(f1))-(exp(FtoR(f1))))<=Fulp(b)(Fexp(f1))/2

  Fexp_error_ulp: LEMMA 
   FORALL(f1|exp_pre(FtoR(f1))):
    abs(FtoR(Fexp(f1)) - exp(FtoR(f1)))
    <= ulp(b)(exp(FtoR(f1)))/2

END std_exp
