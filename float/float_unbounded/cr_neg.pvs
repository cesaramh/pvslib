% Definition of Additive Inverse operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
cr_neg[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  f1,f2:    VAR float
  x1,x2,x3: VAR real

  neg_pre(x1):MACRO bool = TRUE

  IMPORTING unop_em_scheme[radix,b,round,RtoF,neg_pre,-]

  Fneg(f1): {f2: (Fcanonic?(b)) | Closest?(b)(-(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fneg_bounded: LEMMA
    Fbounded?(b)(Fneg(f1))
  AUTO_REWRITE+ Fneg_bounded

  Fneg_error: LEMMA 
    abs(FtoR(Fneg(f1))-(-(FtoR(f1))))<=Fulp(b)(Fneg(f1))/2
    
  % @QED Fneg_error_ulp proved by mmoscato on Tue, 15 Nov 2022 03:19:50 GMT
  Fneg_error_ulp: LEMMA 
   abs(FtoR(Fneg(f1)) - -FtoR(f1)) <= ulp(b)(-(FtoR(f1)))/2

  Fneg_exact: LEMMA
    FORALL(f1:(Fbounded?(b))): FtoR(Fneg(f1)) = -FtoR(f1)

END cr_neg
