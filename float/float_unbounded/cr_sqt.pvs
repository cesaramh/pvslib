%
% Definition of (correctly rounded) square rott.
%
cr_sqt[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING reals@sqrt

  f1,f2:    VAR float
  x1,x2,x3: VAR real

  sqrt_pre(x1):MACRO bool = x1 >= 0

  IMPORTING unop_em_scheme[radix,b,round,RtoF,sqrt_pre,sqrt]

  Fsqrt((f1|sqrt_pre(FtoR(f1)))):
                        {f2: (Fcanonic?(b)) | Closest?(b)(sqrt(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fsqrt_bounded: LEMMA
   FORALL(f1|sqrt_pre(FtoR(f1))):
    Fbounded?(b)(Fsqrt(f1))
  AUTO_REWRITE+ Fsqrt_bounded

  Fsqrt_error: LEMMA
   FORALL(f1|sqrt_pre(FtoR(f1))):
    abs(FtoR(Fsqrt(f1))-(sqrt(FtoR(f1))))<=Fulp(b)(Fsqrt(f1))/2

  Fsqrt_error_ulp: LEMMA 
   FORALL(f1|sqrt_pre(FtoR(f1))):
    abs(FtoR(Fsqrt(f1)) - sqrt(FtoR(f1)))
    <= ulp(b)(sqrt(FtoR(f1)))/2
    
END cr_sqt
