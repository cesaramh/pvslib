accum_err_exp[
  radix : above(1),
  (IMPORTING float[radix])
  b     : Format,
  round: ClosestRoundingPred[radix](b),
  RtoF : { RtoF: CanonicRoundingFun(b,round) |
                 exact_rep_conservation[radix](b,round)(RtoF) }
]: THEORY
BEGIN
  ASSUMING

    round_even_closest: ASSUMPTION
      FORALL(x:real,f:(Fbounded?(b))): round(x,f) IMPLIES EvenClosest?(b)(x,f)

    round_rounded_mode: ASSUMPTION
      rounded_mode?(b)(round)

  ENDASSUMING

  f1 : VAR (Fbounded?(b))
  e1 : VAR nonneg_real
  r1 : VAR real

  IMPORTING std_exp[radix,b,round,RtoF],
  	    reals@abs_lems,
            interval_arith@interval_mps
	
  exp_aerr_bound(r1,e1): nonneg_real =
   (exp(e1)-1)*exp(r1)

  exp_accum_err: LEMMA 
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(exp(FtoR(f1))-exp(r1)) <= exp_aerr_bound(r1,e1)

  exp_ulp_bound(r1,e1): real = exp(r1 + e1)

  Fexp_accum_err_bound: LEMMA
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(exp(FtoR(f1))) <= exp_ulp_bound(r1,e1)  

  IMPORTING accum_err_op1sch[
              (Fbounded?(b)), FtoR, RtoF, ulp(b),
	      LAMBDA(f1): TRUE, Fexp,
	      LAMBDA(r1): TRUE, exp,
	      LAMBDA(r1,e1): TRUE,
	      exp_aerr_bound,
	      LAMBDA(r1,e1): TRUE,
	      exp_ulp_bound] AS exp_prf

  accum_err_bound: LEMMA
    abs(FtoR(f1)-r1) <= e1 
    IMPLIES abs(FtoR(Fexp(f1)) - exp(r1))
            <= exp_aerr_bound(r1,e1)
	       + ulp(b)(exp_ulp_bound(r1,e1)) / 2    

END accum_err_exp
