aerr754sp_mul: THEORY
BEGIN

  IMPORTING ieee754sp_mul,
  	    accum_err_mul[ieee754radix,single_precision_format,sp_closest?,RtoS]

  e1,e2 : VAR nonneg_real
  r1,r2 : VAR real
  
  aeboundsp_mul(r1,e1,r2,e2): nonneg_real =
    mul_aerr_bound(r1,e1,r2,e2) + ulp_sp(mul_ulp_bound(r1,e1,r2,e2))/2
    
  Smul_aerr: LEMMA
   FORALL(e1, e2: nonneg_real, r1, r2: real, f1, f2: unb_single):
    abs(StoR(f1)-r1) <= e1 AND
    abs(StoR(f2)-r2) <= e2
    IMPLIES abs(StoR(Smul(f1,f2))-(r1*r2)) <= aeboundsp_mul(r1,e1,r2,e2)  

  IMPORTING aerr_mul_props[ieee754radix,single_precision_format,sp_closest?,RtoS]

  aeboundsp_mul_p2l(n: nat,e: nonneg_real): nonneg_real = 2^n * e
  aeboundsp_mul_p2r(n: nat,e: nonneg_real): nonneg_real = 2^n * e

  Smulpow2l_aerr: LEMMA
   FORALL(e: nonneg_real,
          r: real,
	  f: unb_single,
	  n: below(ieee754sp_prec) | n < ieee754sp_prec - Fexp(f)):
    abs(StoR(f)-r) <= e
    IMPLIES abs(StoR(Smul(RtoS(2^n),f))-(2^n*r)) <= aeboundsp_mul_p2l(n,e)

  Smulpow2r_aerr: LEMMA
   FORALL(e: nonneg_real,
          r: real,
	  f: unb_single,
	  n: below(ieee754sp_prec) | n < ieee754sp_prec - Fexp(f)):
    abs(StoR(f)-r) <= e
    IMPLIES abs(StoR(Smul(f,RtoS(2^n)))-(r*2^n)) <= aeboundsp_mul_p2r(n,e)

END aerr754sp_mul
