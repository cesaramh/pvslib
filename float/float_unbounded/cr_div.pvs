% Definition of Division operation following the IEEE 754 standard. According
% to the standard, every operation must be performed as if it first produced
% an intermediate result correct to infinite precision and with unbounded range,
% and then rounded to fit the target format, if necessary." [IEEE Std 754-2008]
%
cr_div[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING float[radix]

  f1,f2:    VAR float
  x1,x2,x3: VAR real

  trivial1(x1):     MACRO bool = TRUE    

  div_pre2(x1)(x2): MACRO bool = x2/=0

  IMPORTING binop_em_scheme[radix,b,round,RtoF,trivial1,div_pre2,
                            LAMBDA(x1,(x2|div_pre2(x1)(x2))):x1/x2]
  
  Fdiv(f1,(f2|div_pre2(FtoR(f1))(FtoR(f2)))):{ f3:(Fcanonic?(b))|
			          Closest?(b)(FtoR(f1)/FtoR(f2),f3) } = 
    Fg(f1,f2)

  Fdiv_bounded: LEMMA
   FORALL(f2|div_pre2(FtoR(f1))(FtoR(f2))):
    Fbounded?(b)(Fdiv(f1,f2))
  AUTO_REWRITE+ Fdiv_bounded
  
  Fdiv_error: LEMMA 
   FORALL(f2|div_pre2(FtoR(f1))(FtoR(f2))):
    abs(FtoR(Fdiv(f1,f2)) - (FtoR(f1)/FtoR(f2))) <= Fulp(b)(Fdiv(f1,f2))/2
    
  % @QED Fdiv_error_ulp proved by mmoscato on Tue, 15 Nov 2022 03:13:58 GMT
  Fdiv_error_ulp: LEMMA
   FORALL(f1,(f2|div_pre2(FtoR(f1))(FtoR(f2)))):
    abs(FtoR(Fdiv(f1,f2)) - (FtoR(f1)/FtoR(f2)))
    <= ulp(b)(FtoR(f1)/FtoR(f2))/2
    
END cr_div
