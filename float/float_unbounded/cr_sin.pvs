% Definition of Sine operation following the IEEE 754 standard.
% According to the standard, every operation must be performed as if it first
% produced an intermediate result correct to infinite precision and with
% unbounded range, and then rounded to fit the target format, if necessary.
%
cr_sin[radix:above(1),
       (IMPORTING float[radix])
       b     : Format,
       round : ClosestRoundingPred(b),
       RtoF  : CanonicRoundingFun(b,round)]: THEORY
BEGIN

  IMPORTING trig@trig_basic

  f1: VAR float
  x1: VAR real

  sin_pre(x1):MACRO bool = TRUE

  IMPORTING unop_em_scheme[radix,b,round,RtoF,sin_pre,sin]
  
  Fsin(f1): {f2: (Fcanonic?(b)) | Closest?(b)(sin(FtoR(f1)),f2) } =
    Fg(f1)
    
  Fsin_bounded: LEMMA
    Fbounded?(b)(Fsin(f1))
  AUTO_REWRITE+ Fsin_bounded

  Fsin_error: LEMMA
    abs(FtoR(Fsin(f1))-(sin(FtoR(f1))))<=Fulp(b)(Fsin(f1))/2

  Fsin_error_ulp: LEMMA 
   FORALL(f1|sin_pre(FtoR(f1))):
    abs(FtoR(Fsin(f1)) - sin(FtoR(f1)))
    <= ulp(b)(sin(FtoR(f1)))/2

END cr_sin
