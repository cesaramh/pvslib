extended_float_qlt
[ radix     : above(1),
  precision : above(1),
  dExp      : { d: posnat | d > 2*(precision-1) - 1 }
] : THEORY BEGIN

  
  IMPORTING extended_float_operations_support[radix,precision,dExp]

  qlt_non_finite(args: [efloat,efloat] | NOT is_finite?(args`1) OR NOT is_finite?(args`2)): bool
  = LET x = args`1, y = args`2
    IN
    IF nan?(x) OR nan?(y) THEN FALSE
    ELSE NOT(x = posinf) AND NOT(y = neginf)
    ENDIF

  qlt_zero(args: [(is_finite?), (is_finite?)] | zero?(args`1) OR zero?(args`2)): bool
  = LET x = args`1, y = args`2 IN
    IF zero?(x) THEN nzpfinite?(y)
    ELSE nznfinite?(x) ENDIF

  IMPORTING ieee754_predicate_scheme__binary[
  	      efloat,
	      is_numeric?,
	      zero?,
	      is_finite?,
	      prj_ef,
	      LAMBDA(x,y:real):x<y,
	      qlt_non_finite,
	      qlt_zero]

  qlt_ed(X,Y: efloat): bool = pred_fp(X,Y)

END extended_float_qlt
