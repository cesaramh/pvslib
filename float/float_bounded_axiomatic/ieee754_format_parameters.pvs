ieee754_format_parameters: THEORY
BEGIN

  % the radix, 2 or 10.
  ieee754_radix: TYPE+ = { i: posint | i=2 OR i=10 }

  ieee754_subtype_above_1
  : judgement
    ieee754_radix subtype_of above(1)

  % the number of digits in the significand (precision)
  ieee754_precision(b: ieee754_radix): TYPE+
  = { p: posint | (b=2 IMPLIES (p=24 OR p=53 OR p=113)) AND
      	       (b=10 IMPLIES (p=16 OR p=34)) }

  ieee754_precision_subtype_above_1
  : judgement
    ieee754_precision(b: ieee754_radix) subtype_of above(1)

  % the maximum exponent e
  ieee754_maxExp(b: ieee754_radix, p: ieee754_precision(b)): TYPE+
  = { emax : nat | (b=2
      	     	    IMPLIES (p=24  IMPLIES emax=127) AND
		    	    (p=53  IMPLIES emax=1023) AND
			    (p=113 IMPLIES emax=16383)) AND
	           (b=10
      	     	    IMPLIES (p=16  IMPLIES emax=384) AND
		    	    (p=34  IMPLIES emax=6144)) }

  ieee754_maxExp_subtype_above_1
  : judgement
    ieee754_maxExp(b: ieee754_radix, p: ieee754_precision(b)) subtype_of above(1)

  % the minimum exponent e. It shall be 1-emax for all formats
  ieee754_minExp(b: ieee754_radix, p: ieee754_precision(b)): TYPE+
  = { emin : negint | FORALL(emax: ieee754_maxExp(b,p)): emin = 1-emax }

END ieee754_format_parameters
