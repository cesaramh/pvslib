ieee754_add
[ (IMPORTING ieee754_format_parameters)
  b    : ieee754_radix,
  p    : ieee754_precision(b),
  emax : ieee754_maxExp(b,p) ]
: THEORY BEGIN

  IMPORTING ieee754_semantics[b,p,emax] AS sem_th

  % addition

  add_ieee754: [datum,datum->datum]

  add_correct__finite
  : AXIOM FORALL(f1: (finite?),f2: (finite?) | nzfinite?(f1) OR nzfinite?(f2))
    : add_ieee754(f1,f2) = round(proj(f1) + proj(f2))

  add_correct__nan
  : AXIOM FORALL(f1,f2: datum): (NaN?(f1) OR NaN?(f2)) IMPLIES NaN?(add_ieee754(f1,f2))

  add_correct__nInf_nInf  : AXIOM add_ieee754(nInf,nInf) = nInf
  add_correct__nInf_finite: AXIOM FORALL(f:(finite?)): add_ieee754(nInf,f) = nInf
  add_correct__nInf_pInf  : AXIOM NaN?(add_ieee754(nInf,pInf))

  add_correct__finite_nInf: AXIOM FORALL(f:(finite?)): add_ieee754(f,nInf) = nInf
  add_correct__finite_pInf: AXIOM FORALL(f:(finite?)): add_ieee754(f,pInf) = pInf

  add_correct__nZero_nZero : AXIOM add_ieee754(nZero,nZero) = nZero
  add_correct__nZero_pZero : AXIOM add_ieee754(nZero,pZero) = pZero
  add_correct__pZero_pZero : AXIOM add_ieee754(pZero,pZero) = pZero
  add_correct__pZero_nZero : AXIOM add_ieee754(pZero,nZero) = pZero

  add_correct__pZero_finite : AXIOM FORALL(f: datum | finite?(f) AND f /= nZero): add_ieee754(pZero,f) = f
  add_correct__nZero_finite : AXIOM FORALL(f:(finite?)): add_ieee754(nZero,f) = f

  add_correct__pInf_nInf  : AXIOM NaN?(add_ieee754(pInf,nInf))
  add_correct__pInf_finite: AXIOM FORALL(f:(finite?)): add_ieee754(pInf,f) = pInf
  add_correct__pInf_pInf  : AXIOM add_ieee754(pInf,pInf) = pInf

  add_finite_def: LEMMA  FORALL(f1,f2: (finite?))
  : safe_proj(add_ieee754(f1, f2)) =
    safe_proj(round(safe_proj(f1) + safe_proj(f2)))

  add_finites_is_finite: LEMMA  FORALL(f1, f2: datum)
  : finite?(add_ieee754(f1,f2)) => finite?(f1) AND finite?(f2)

  finite?_projs_finite?_add
  : THEOREM
    âˆ€(f1,f2: (finite?))
    : finite?(round(proj(f1) + proj(f2)))
    => finite?(add_ieee754(f1,f2))

END ieee754_add
