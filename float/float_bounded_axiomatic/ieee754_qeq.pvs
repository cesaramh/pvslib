%
% Quiet Equal to Comparison
%
ieee754_qeq
[ (IMPORTING ieee754_format_parameters)
  b    : ieee754_radix,
  p    : ieee754_precision(b),
  emax : ieee754_maxExp(b,p) ]
: THEORY BEGIN

  IMPORTING ieee754_semantics[b,p,emax] AS sem_th

  qeq_ieee754: [datum,datum->bool]

  qeq_correct__finite
  : AXIOM FORALL(f1: (finite?),f2: (finite?) | nzfinite?(f1) OR nzfinite?(f2))
    : qeq_ieee754(f1,f2) = (proj(f1) = proj(f2))

  qeq_correct__nan
  : AXIOM FORALL(f1,f2: datum): NaN?(f1) OR NaN?(f2) IMPLIES qeq_ieee754(f1,f2) = FALSE

  % Comparisons shall ignore the sign of zero (so +0 = -0)
  % [IEEE-754-2019, Section 5.11 Details of comparison predicates]
  qeq_correct__nZero_nZero  : AXIOM qeq_ieee754(nZero,nZero) = TRUE
  qeq_correct__nZero_pZero  : AXIOM qeq_ieee754(nZero,pZero) = TRUE
  qeq_correct__pZero_nZero  : AXIOM qeq_ieee754(pZero,nZero) = TRUE
  qeq_correct__pZero_pZero  : AXIOM qeq_ieee754(pZero,pZero) = TRUE

  qeq_correct__nInf_nnan    : AXIOM FORALL(f:datum| NOT NaN?(f)): qeq_ieee754(nInf,f) = (f=nInf)
  qeq_correct__nnan_nInf    : AXIOM FORALL(f:datum| NOT NaN?(f)): qeq_ieee754(f,nInf) = (f=nInf)

  qeq_correct__pInf_nnan    : AXIOM FORALL(f:datum| NOT NaN?(f)): qeq_ieee754(pInf,f) = (f=pInf)
  qeq_correct__nnan_pInf    : AXIOM FORALL(f:datum| NOT NaN?(f)): qeq_ieee754(f,pInf) = (f=pInf)

  % @QED qeq_finite_equiv proved by ltitolo on Tue, 21 Nov 2023 14:56:30 GMT
  qeq_finite_equiv
  : LEMMA
    ∀(f1: (finite?), f2: datum): qeq_ieee754(f1, f2) ⇒ finite?(f2)

  qeq_finite_def: LEMMA
  FORALL(f1,f2: (finite?))
  : qeq_ieee754(f1, f2) =
    (proj(f1) = proj(f2))

  % @QED qeq_number_symmetric proved by ltitolo on Tue, 21 Nov 2023 15:14:41 GMT
  qeq_number_symmetric
  : LEMMA
    ∀(f: (number?)): qeq_ieee754(f,f)

END ieee754_qeq
