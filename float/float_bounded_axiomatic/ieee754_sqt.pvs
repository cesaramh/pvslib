ieee754_sqt
[ (IMPORTING ieee754_format_parameters)
  b    : ieee754_radix,
  p    : ieee754_precision(b),
  emax : ieee754_maxExp(b,p) ]
: THEORY BEGIN

  IMPORTING ieee754_semantics[b,p,emax] AS sem_th

  sqt_ieee754: [datum->datum]

  IMPORTING reals@sqrt

  % This definition is here to force PVS to add sqt_correct__finite
  % as a TCC when this theory is interpreted.
  sqrt(nnx: nonneg_real): {nnz : nnreal | nnz*nnz = nnx} = sqrt(nnx)

  sqt_correct__finite
  : AXIOM FORALL(f1: (pfinite?))
    : sqt_ieee754(f1) = round(sqrt(proj(f1)))

  sqt_correct__nan
  : AXIOM FORALL(f1: datum): NaN?(f1) IMPLIES NaN?(sqt_ieee754(f1))

  sqt_correct__nInf    : AXIOM NaN?(sqt_ieee754(nInf))
  sqt_correct__nfinite : AXIOM FORALL(f:(nfinite?)): NaN?(sqt_ieee754(f))
  sqt_correct__nZero   : AXIOM sqt_ieee754(nZero) = nZero % A negative zero is allowed as an operand as per IEEE 754 ยง5.4.1.
  sqt_correct__pZero   : AXIOM sqt_ieee754(pZero) = pZero
  sqt_correct__pInf    : AXIOM sqt_ieee754(pInf) = pInf

END ieee754_sqt
