%
% This theory defines the sets of floating-point data
%
ieee754_data
[ (IMPORTING ieee754_format_parameters)
  b    : ieee754_radix,
  p    : ieee754_precision(b),
  emax : ieee754_maxExp(b,p) ]
  % These formal parameters are not explicitly used, but they force the existence
  % of one theory for each combination of actual parameters.
: THEORY BEGIN
  %
  %   The type 'datum' denotes all floating-point data representable in the
  % format determined by the theory parameters. Such data comprises:
  % - signed zero and non-zero numbers (called finite data)
  % - two infinites (positive and negative)
  % - two NaNs (quit and signaling)
  %
  datum : TYPE+

  pInf : datum % positive infinity
  nInf : datum % negative infinity
  finite?(f:datum): bool
  NaN? : PRED[datum]

  pinf_not_finite: AXIOM NOT finite?(pInf)
  pinf_not_NaN   : AXIOM NOT NaN?(pInf)

  ninf_not_finite: AXIOM NOT finite?(nInf)
  ninf_not_NaN   : AXIOM NOT NaN?(nInf)

  nan_not_finite : AXIOM FORALL(nan:(NaN?)): NOT finite?(nan)

  pInf_nInf_Nan_finite
  : AXIOM
    FORALL(f: datum)
    : (f /= pInf AND f /= nInf AND NOT NaN?(f))
      IFF finite?(f)

  number?(f: datum): MACRO bool = NOT NaN?(f)

  % Zeros are finite
  pZero: datum % positive zero
  pzero_finite: AXIOM finite?(pZero)
  AUTO_REWRITE+ pzero_finite

  nZero: datum % negative zero
  nzero_finite: AXIOM finite?(nZero)
  AUTO_REWRITE+ nzero_finite

END ieee754_data
