time_tictoc : THEORY
BEGIN

  real_time : nat % Realtime in internal units
  run_time  : nat % Realtime in internal units
  internal_time_units : posreal % Internal units

  Stamp : Global[[nat,nat],(0,0)]  % (realtime,runtime)

  tic : void =
    set[[nat,nat]](Stamp,(real_time,run_time))

  Toc : TYPE = [nnreal,nnreal]
  
  %% Return times in seconds
  toc : Toc = 
    LET (ret,rut) = (real_time,run_time),
        (re0,ru0) = val[[nat,nat]](Stamp) IN
      (max(0,(ret-re0))/internal_time_units,
       max(0,(rut-ru0)/internal_time_units))

  % t is in secods; returns formatted string in the form "<h>h:<m>m:<s>s"
  hms(t:real) : string =
    LET ts = floor(t),
        ms = t-ts,
        h = floor(ts/3600),
        m = floor(mod(ts,3600)/60),
        s = mod(mod(ts,3600),60)+ms IN
    format("~ah:~am:~,3fs",{|h,m,s|})

  str_toc(atoc:Toc) : MACRO string =
    LET (realtime,runtime) = atoc IN 
    format("Real Time: ~a (~,3f sec). Run Time: ~a (~,3f sec)~%",
	  {| hms(realtime), realtime, hms(runtime), runtime |})

END time_tictoc
